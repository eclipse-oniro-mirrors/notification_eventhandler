# Copyright (c) 2021-2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/ohos/ace/ace.gni")
import("../../eventhandler.gni")

config("public_emitter_config") {
  include_dirs = [ "include" ]
}

if (is_arkui_x) {
  import("//plugins/events/eventhandler.gni")
  ohos_source_set("emitter") {
    include_dirs = [
      "include",
      "//third_party/node/src",
      "//third_party/libuv/include",
    ]
    include_dirs += emitter_include
    sources = [
      "src/events_emitter.cpp",
      "src/init.cpp",
      "src/interops.cpp",
    ]
  }
} else {
  ohos_shared_library("emitter") {
    sanitize = {
      integer_overflow = true
      ubsan = true
      boundary_sanitize = true
      cfi = true
      cfi_cross_dso = true
      debug = false
    }
    branch_protector_ret = "pac_ret"

    sources = [
      "src/init.cpp",
    ]

    deps = [ "${frameworks_path}/napi:emitter_interops" ]

    external_deps = [
      "c_utils:utils",
      "napi:ace_napi",
    ]

    relative_install_dir = "module/events"
    subsystem_name = "notification"
    part_name = "eventhandler"
  }

  ohos_shared_library("emitter_interops") {
    sanitize = {
      integer_overflow = true
      ubsan = true
      boundary_sanitize = true
      cfi = true
      cfi_cross_dso = true
      debug = false
    }
    branch_protector_ret = "pac_ret"
    version_script = "libemitter.map"

    include_dirs = [
      "include",
      "${inner_api_path}",
    ]

    public_configs = [ ":public_emitter_config" ]

    sources = [
      "src/events_emitter.cpp",
      "src/interops.cpp"
    ]

    deps = [ "${frameworks_path}/eventhandler:libeventhandler" ]

    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
      "napi:ace_napi",
    ]

    subsystem_name = "notification"
    part_name = "eventhandler"
  }
}
